plugins {
	id 'java'
	id 'org.springframework.boot' version '4.0.1'
	id 'io.spring.dependency-management' version '1.1.7'
	id 'jacoco'
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'
description = 'BeGroom'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(25)
	}
}

repositories {
	mavenCentral()
}

jacoco {
	toolVersion = "0.8.14"
	//  default directory: "$/jacoco"
}

jacocoTestCoverageVerification {
	violationRules {
		rule {
			element = 'CLASS'

			limit {
				counter = 'LINE'
				value = 'COVEREDRATIO'
				// minimum = 0.80
			}

			excludes = [
					"**.*Application*",    // 메인 애플리케이션 클래스
					"**.domain.**",		   // 도메인 클래스
					"**.*Request*",        // DTO성 클래스
					"**.*Response*",       // DTO성 클래스
					"**.dto.**",           // DTO 패키지
					"**.*Config*",         // Spring Batch 등 각종 설정 파일
					"**.*Condition*",      // 조건부 설정 파일
					"**.*Q*",              // QueryDSL 생성 클래스
					"**/Q*.class",         // QueryDSL 생성 클래스
					"**.Q**"               // QueryDSL 클래스
			]
		}
	}
}

jacocoTestReport {
	reports {
		html.required = true
		xml.required = true
	}

	finalizedBy 'jacocoTestCoverageVerification'
}

test {
	useJUnitPlatform()

	finalizedBy jacocoTestReport
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.boot:spring-boot-starter-security'

	runtimeOnly 'com.mysql:mysql-connector-j'
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'

	implementation 'io.jsonwebtoken:jjwt-api:0.11.5'
	runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.11.5'
	runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.11.5'

	implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:3.0.0'
	runtimeOnly 'com.h2database:h2'

	// h2
	testRuntimeOnly 'com.h2database:h2'

	// test
	testImplementation 'org.springframework.boot:spring-boot-starter-webmvc-test'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'

	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
	implementation 'org.springframework.boot:spring-boot-starter-webflux'

	// load-test
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'io.micrometer:micrometer-registry-influx'

	//QueryDsl
	implementation 'com.querydsl:querydsl-jpa:5.0.0:jakarta'
	implementation "com.querydsl:querydsl-core"
	implementation "com.querydsl:querydsl-collections"
	annotationProcessor "com.querydsl:querydsl-apt:5.0.0:jakarta"
	annotationProcessor "jakarta.annotation:jakarta.annotation-api"
	annotationProcessor "jakarta.persistence:jakarta.persistence-api"
	annotationProcessor "com.querydsl:querydsl-apt:${dependencyManagement.importedProperties['querydsl.version']}:jpa"

	implementation 'net.datafaker:datafaker:2.4.0'
}

// Querydsl 설정부
def generated = 'build/generated/sources/annotationProcessor/java/main/QClass'

// querydsl QClass 파일 생성 위치 지정
tasks.withType(JavaCompile){
	options.getGeneratedSourceOutputDirectory().set(file(generated))
}

tasks.named('test') {
	useJUnitPlatform()
}
